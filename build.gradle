buildscript {
	repositories {
		mavenCentral()
		maven {
			name 'Forge'
			url 'https://maven.minecraftforge.net'
		}
		maven {
			name 'Mixin'
			url 'https://repo.spongepowered.org/maven'
		}
	}
	dependencies {
		classpath 'com.anatawa12.forge:ForgeGradle:2.3-1.0.7'
		classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'

// Add version to the jar name
version project.version

minecraft {
	version = '1.12.2-14.23.5.2847'
	mappings = 'stable_39'
	runDir = 'run'

	def args = ['-Dfml.coreMods.load=io.redstudioragnarok.fbp.asm.FBPPlugin', '-Dforge.logging.console.level=debug']

	replace('@ID@', project.id)
	replace('@VERSION@', project.version)
	replaceIn('ModReference.java')
}

runClient {
	args '--username=FancierDev'
}

// Replace version in `mcmod.info`
processResources {
	inputs.property 'id', project.id
	inputs.property 'version', project.version
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'id': project.id, 'version': project.version
	}
}

// Create configuration to include embedded dependencies into FBP jar
configurations {
	implementation
	compile.extendsFrom(embed)
}

repositories {
	maven {
		name 'MixinBooter'
		url 'https://maven.cleanroommc.com'
	}

	ivy {
		url 'https://github.com/'

		layout "pattern", { artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]' }

		metadataSources { artifact() }
	}

	ivy {
		url 'https://github.com/'

		layout "pattern", { artifact '[organisation]/[module]/releases/download/[revision]/[module]-[revision].[ext]' }

		metadataSources { artifact() }
	}
}

dependencies {
	implementation 'zone.rong:mixinbooter:7.0'
	implementation 'Red-Studio-Ragnarok:Red-Core:0.2-Dev-2@jar'

	implementation 'net.jafama:jafama:2.+'
	embed 'net.jafama:jafama:2.+'

	implementation 'meldexun:MatrixUtil:1.2.0@jar'
	embed 'meldexun:MatrixUtil:1.2.0@jar'
}

sourceSets {
	main {
		ext.refMap = 'mixins.fbp.refmap.json'
	}
}

jar {
	// Add all embedded dependencies into the jar
	from(configurations.embed.collect {it.isDirectory() ? it : zipTree(it)})

	manifest.attributes([
			'ModSide': 'CLIENT',
			'FMLAT': 'fbp_at.cfg',
			'FMLCorePlugin': 'io.redstudioragnarok.fbp.asm.FBPPlugin',
			'FMLCorePluginContainsFMLMod': 'true',
			'ForceLoadAsMod': 'true'
	])
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

idea {
	module {
		excludeDirs = [file(".github"), file(".gradle"), file(".idea"), file("build"), file("gradle"), file("run")]
	}
}
